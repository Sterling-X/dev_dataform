config {
  type: "table",
  database: "rc-datamart-adminreport",
  schema: "analytics_vasquez_de_lara", 
  name: "fct_vdl_local_seo_solv", 
  tags: ["local seo", "solv", "vdl", "vasquez de lara"]
}

/*
  Changelog: yyyy-mm-dd jbatoy: 
*/

WITH cte_current AS (
  SELECT
    location
    , ROUND(AVG(solv), 4) AS avg_current_solv
  FROM ${ref("rc-datamart-adminreport", "vasquez_de_lara", "stg_vdl_local_brand_manager_geogrids")}
  WHERE date = DATE_ADD(
    DATE_TRUNC(CURRENT_DATE(), MONTH),
    INTERVAL MOD(9 - EXTRACT(DAYOFWEEK FROM DATE_TRUNC(CURRENT_DATE(), MONTH)), 7) DAY
  )
  GROUP BY location
)

, cte_prior_90_days AS (
  SELECT
    location
    , ROUND(AVG(solv), 4) AS avg_prior_90_days_solv
  FROM ${ref("rc-datamart-adminreport", "vasquez_de_lara", "stg_vdl_local_brand_manager_geogrids")}
  WHERE date = DATE_ADD(
    DATE_TRUNC(DATE_SUB(CURRENT_DATE(), INTERVAL 90 DAY), MONTH),
    INTERVAL MOD(9 - EXTRACT(DAYOFWEEK FROM DATE_TRUNC(DATE_SUB(CURRENT_DATE(), INTERVAL 90 DAY), MONTH)), 7) DAY
  )
  GROUP BY location
)

, cte_prior_year AS (
  SELECT
    location
    , ROUND(AVG(solv), 4) AS avg_prior_year_solv
  FROM ${ref("rc-datamart-adminreport", "vasquez_de_lara", "stg_vdl_local_brand_manager_geogrids")}
  WHERE date = DATE_ADD(
    DATE_TRUNC(DATE_SUB(CURRENT_DATE(), INTERVAL 1 YEAR), MONTH),
    INTERVAL MOD(
      9 - EXTRACT(DAYOFWEEK FROM DATE_TRUNC(DATE_SUB(CURRENT_DATE(), INTERVAL 1 YEAR), MONTH)),
      7
    ) DAY
  )
  GROUP BY location
)

SELECT
  c.location
  , y.avg_prior_year_solv
  , d.avg_prior_90_days_solv
  , c.avg_current_solv
  , CASE
      WHEN LOWER(c.location) = 'kendall' THEN 0.75
      WHEN LOWER(c.location) = 'coral gables' THEN 0.75
      WHEN LOWER(c.location) = 'downtown miami' THEN 0.25
    END AS target_solv
FROM cte_current c
LEFT JOIN cte_prior_90_days d ON c.location = d.location
LEFT JOIN cte_prior_year y ON c.location = y.location
