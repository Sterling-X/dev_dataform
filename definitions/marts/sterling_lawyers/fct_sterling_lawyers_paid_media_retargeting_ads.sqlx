config {
  type: "table",
  database: "rc-datamart-adminreport",
  schema: "analytics_sterling_lawyers", 
  name: "fct_sterling_lawyers_paid_media_retargeting_ads", 
  tags: ["paid media", "retargeting ads", "sterling lawyers"]
}

/*
  Changelog: yyyy-mm-dd jbatoy: 
*/

WITH cte_ranges AS (
  SELECT
    DATE_SUB(CURRENT_DATE(), INTERVAL 1 YEAR) AS prior_year_start
    , CURRENT_DATE() AS today
    , DATE_SUB(CURRENT_DATE(), INTERVAL 90 DAY) AS last_90_start
    , DATE_SUB(CURRENT_DATE(), INTERVAL 180 DAY) AS prior_90_start
)

, cte_retargeting_ads AS (
  SELECT
    date
    , SUM(spend) AS spend
    , SUM(leads) AS leads
  FROM (
    SELECT
      date
      , spend
      , actions_offsite_conversion_fb_pixel_custom AS leads
    FROM ${ref("rc-datamart-adminreport", "sterling_lawyers", "stg_sterling_lawyers_facebook_ads")}
    -- WHERE campaign IN (
    --   'RC - Retargeting - Leads',
    --   'RC - UCD - Video Ads'
    -- )

    UNION ALL

    SELECT
      date
      , spend
      , conversions AS leads
    FROM ${ref("rc-datamart-adminreport", "sterling_lawyers", "stg_sterling_lawyers_stackadapt")}
    -- WHERE campaign = 'Brand Remarketing (Website Visitors) - Audio'
  )
  GROUP BY date
)

, cte_metrics AS (
  SELECT
    -- spend
    SUM(CASE WHEN s.date BETWEEN r.prior_year_start AND r.today THEN s.spend END) AS spend_prior_year
    , SUM(CASE WHEN s.date BETWEEN r.prior_90_start AND r.last_90_start THEN s.spend END) AS spend_prior_90_days
    , SUM(CASE WHEN s.date BETWEEN r.last_90_start AND r.today THEN s.spend END) AS spend_last_90_days

    -- leads
    , SUM(CASE WHEN s.date BETWEEN r.prior_year_start AND r.today THEN s.leads END) AS leads_prior_year
    , SUM(CASE WHEN s.date BETWEEN r.prior_90_start AND r.last_90_start THEN s.leads END) AS leads_prior_90_days
    , SUM(CASE WHEN s.date BETWEEN r.last_90_start AND r.today THEN s.leads END) AS leads_last_90_days
  FROM cte_retargeting_ads s
  CROSS JOIN cte_ranges r
)

SELECT * FROM (
  SELECT
    'Spend' AS metric
    , ROUND(spend_prior_year, 4) AS prior_year
    , ROUND(spend_prior_90_days, 4) AS prior_90_days
    , ROUND(spend_last_90_days, 4) AS last_90_days
  FROM cte_metrics

  UNION ALL

  SELECT
    'Leads' AS metric
    , ROUND(leads_prior_year, 4) AS prior_year
    , ROUND(leads_prior_90_days, 4) AS prior_90_days
    , ROUND(leads_last_90_days, 4) AS last_90_days
  FROM cte_metrics

  UNION ALL

  SELECT
    'Cost per Lead (CPL)' AS metric
    , ROUND(SAFE_DIVIDE(spend_prior_year, leads_prior_year), 4) AS prior_year
    , ROUND(SAFE_DIVIDE(spend_prior_90_days, leads_prior_90_days), 4) AS prior_90_days
    , ROUND(SAFE_DIVIDE(spend_last_90_days, leads_last_90_days), 4) AS last_90_days
  FROM cte_metrics
)
