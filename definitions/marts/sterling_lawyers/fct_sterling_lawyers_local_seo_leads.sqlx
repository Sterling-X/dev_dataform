config {
  type: "table",
  database: "rc-datamart-adminreport",
  schema: "analytics_sterling_lawyers", 
  name: "fct_sterling_lawyers_local_seo_leads", 
  tags: ["local seo", "leads", "sterling lawyers"]
}

/*
  Changelog: yyyy-mm-dd jbatoy: 
*/

WITH cte_leads AS (
  SELECT
    FORMAT_DATE('%Y%m', date) AS year_month
    , COUNTIF(call_status = 'answered') AS cnt_answered
    FROM ${ref("rc-datamart-adminreport", "sterling_lawyers", "stg_sterling_lawyers_call_tracking")}
    GROUP BY year_month
)

, cte_ranges AS (
  SELECT
    n
    , DATE_TRUNC(DATE_SUB(CURRENT_DATE(), INTERVAL n MONTH), MONTH) AS last_12_months
    , FORMAT_DATE('%Y%m', DATE_TRUNC(DATE_SUB(CURRENT_DATE(), INTERVAL n MONTH), MONTH)) AS last_12_months_ym
    , FORMAT_DATE('%Y%m', DATE_TRUNC(DATE_SUB(DATE_SUB(CURRENT_DATE(), INTERVAL n MONTH), INTERVAL 12 MONTH), MONTH)) AS prior_12_months_ym
  FROM UNNEST(GENERATE_ARRAY(0, 11)) AS n
)

SELECT
  r.n
  , r.last_12_months
  , l.cnt_answered AS last_12_months_total
  , p.cnt_answered AS prior_12_months_total
FROM cte_ranges r
LEFT JOIN cte_leads l ON l.year_month = r.last_12_months_ym
LEFT JOIN cte_leads p ON p.year_month = r.prior_12_months_ym
